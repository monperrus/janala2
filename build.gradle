
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'eclipse'

// In this section you declare where to find the dependencies of your project
repositories {
  jcenter()
}

sourceSets {
  main {
    java { exclude 'src/integration' }
  }

  integration {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir 'src/integration/java'
    }
    groovy {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir 'src/integration/groovy'
    }
  }
}


configurations {
  integrationCompile.extendsFrom testCompile
  integrationRuntime.extendsFrom testRuntime
}

// In this section you declare the dependencies for your production and test code
dependencies {
  compile 'org.ow2.asm:asm-all:5.0.4'  // Bytecode manipulation
  compile 'org.choco-solver:choco-solver:3.3.1'
  compile 'dk.brics.automaton:automaton:1.11-8' // Used for regex
  compile 'junit:junit:4.12' // Used for assertions

  testCompile 'junit:junit:4.12'
  testCompile 'org.codehaus.groovy:groovy-all:2.4.5'
  testCompile 'org.mockito:mockito-core:1.10.19'
}

apply plugin: "jacoco"
jacoco { toolVersion = "0.7.5.201505241946" }

version = '0.2'
jar {
  manifest { attributes 'Premain-Class': 'janala.instrument.SnoopInstructionTransformer' }
}

task integrationTest (type: Test) {
  testClassesDir = sourceSets.integration.output.classesDir
  classpath = sourceSets.integration.runtimeClasspath
}
